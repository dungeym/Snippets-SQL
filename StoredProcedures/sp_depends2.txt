DECLARE @Name NVARCHAR(255)
SET @Name = 'tblConfirmationPopulateRequest'

SELECT 
	[TableName] = o.name
	,[Owner] = s.name
	,[Type] = o.type_desc
	,CreatedDate = o.create_date
	,ModifiedDate = o.modify_date
FROM sys.objects o WITH (NOLOCK)
INNER JOIN sys.schemas s WITH (NOLOCK) ON s.[schema_id] = o.[schema_id]
WHERE o.[name] = @Name


SELECT
	[ColumnName] = c.name
	,ColumnId = c.column_id
	,SystemType = (SELECT [name] FROM sys.types t WHERE t.system_type_id = c.system_type_id AND t.user_type_id = c.user_type_id)
	,UserType = (SELECT [name] FROM sys.types t WHERE t.user_type_id = c.user_type_id)
	,[Length] = c.max_length
	,[Precision] = c.[precision]
	,[Scale] = c.[scale]
	,[CollationName] = c.collation_name
	,IsIdentity = c.is_identity
	,IsPrimaryKey = CASE WHEN (SELECT COUNT(1) FROM sys.index_columns ic WITH (NOLOCK) INNER JOIN sys.indexes i WITH (NOLOCK) ON i.[object_id] = ic.[object_id] AND ic.index_id = i.index_id WHERE i.[object_id] = OBJECT_ID(@Name) AND i.is_primary_key = 1 AND ic.column_id = c.column_id) > 0 THEN 1 ELSE 0 END
	,IsNullable = c.is_nullable
	,[IsUserDefined] = t.is_user_defined
	,IsComputed = c.is_computed
	,HasDefault = CASE WHEN c.default_object_id = 0 THEN 0 ELSE 1 END
	,[MaxLength] = t.max_length
	,[MaxPrecision] = t.[precision]
	,[MaxScale] = t.[scale]
	,[Owner] = s.[name]
FROM sys.columns c WITH (NOLOCK)
INNER JOIN sys.types t WITH (NOLOCK) ON c.system_type_id = t.system_type_id AND t.user_type_id = c.user_type_id
INNER JOIN sys.schemas s WITH (NOLOCK) ON s.[schema_id] = t.[schema_id]
WHERE 1=1
AND c.[object_id] = OBJECT_ID(@Name)
ORDER BY c.column_id ASC 


SELECT
	[IndexName] = i.name
	,[Type] = i.type_desc
	,IsPrimaryKey = i.is_primary_key
	,IsUniqueConstraint = i.is_unique_constraint
	,IsDisabled = i.is_disabled
FROM sys.indexes i WITH (NOLOCK)
WHERE 1=1
AND i.[object_id] = OBJECT_ID(@Name)


SELECT
	[Has reference to Table] = OBJECT_NAME(k.referenced_object_id)
	,[ConstraintName] = k.[name]
	,[ReferenceType] = k.type_desc 
	,[CreatedDate] = k.create_date
	,[ModifiedDate] = k.modify_date
	,[IsDisabled] = k.is_disabled	
	,[UpdateAction] = k.update_referential_action_desc
	,[DeleteAction] = k.delete_referential_action_desc
FROM sys.foreign_keys k WITH (NOLOCK)
INNER JOIN sys.schemas s WITH (NOLOCK) ON s.[schema_id] = k.[schema_id]
WHERE 1=1
AND k.parent_object_id = OBJECT_ID(@Name) 


SELECT
	[Is referenced by Table] = OBJECT_NAME(k.parent_object_id)
	,[ConstraintName] = k.[name]
	,[ReferenceType] = k.type_desc 
	,[CreatedDate] = k.create_date
	,[ModifiedDate] = k.modify_date
	,[IsDisabled] = k.is_disabled	
	,[UpdateAction] = k.update_referential_action_desc
	,[DeleteAction] = k.delete_referential_action_desc
FROM sys.foreign_keys k WITH (NOLOCK)
INNER JOIN sys.schemas s WITH (NOLOCK) ON s.[schema_id] = k.[schema_id]
WHERE 1=1
AND k.referenced_object_id = OBJECT_ID(@Name) 

EXEC sp_depends @Name

